external procedure DOLMakeWebRequest(string , string , area , var area );

global
updating procedure DOLGetShopifyProdIDMn(record RcVc RepSpec)
begin
  record INVc INr,IN2r;
  record DOLShopifyBlock DOLShopifyBl;
  json jdata;
  boolean breakf;
  integer i,loopc,pg;
  string 255 data,since_id,pub_stat;
  val price;
  area a_req,a_reply;

  BlockLoad(DOLShopifyBl);

  pub_stat = "published";
  if (RepSpec.flags[0] == 1) then begin
    pub_stat = "any";
  end;

  if (RepSpec.ArtMode == 0 or RepSpec.ArtMode == 2) then begin // EVS: DOL-167 artmode == 2
    if (nonblank(DOLShopifyBl.APIUser) and nonblank(DOLShopifyBl.APIPass)) then begin
      
      pg = 1;
      DOLMakeWebRequest("GET","/admin/products.json?fields=id,variants&published_status=" & pub_stat & "&limit=250",a_req,a_reply);

      jdata = ParseJSONArea(a_reply);

      while (GetAreaLength(a_reply) > 0 and JSONNodeExists(jdata,"products/[0]/id") and loopc < 100) begin // 25000 items max; to avoid infinite loop

        i = 0;
        while (JSONNodeExists(jdata,"products/[" & i & "]/id")) begin
          if (JSONNodeExists(jdata,"products/[" & i & "]/variants/[0]/sku")) then begin
            data = JSONGet(jdata,"products/[" & i & "]/variants/[0]/sku");
            since_id = JSONGet(jdata,"products/[" & i & "]/id");
            INr.Code = data;
            if (ReadFirstMain(INr,1,true)) then begin
              if (RepSpec.ArtMode == 0) then begin
                data = JSONGet(jdata,"products/[" & i & "]/id");
                if (INr.ShopifyID != data and nonblank(data)) then begin
                  RecordCopy(IN2r,INr);
                  INr.ShopifyID = data;
                  RecordUpdate(IN2r,INr,false);
                end;
              end;
              // EVS: DOL-167
              if (RepSpec.ArtMode == 2) then begin
                data = JSONGet(jdata,"products/[" & i & "]/variants/[0]/price");
                price = StringToVal(data,M4Val);
                if (INr.ShopifyPrice != price and nonblank(data)) then begin
                  RecordCopy(IN2r,INr);
                  INr.ShopifyPrice = price;
                  RecordUpdate(IN2r,INr,false);
                end;
              end;
              // :EVS
            end;
          end;
          i = i + 1;
        end;

        SetAreaZeroSize(a_reply);

        pg = pg + 1;
        DOLMakeWebRequest("GET","/admin/products.json?fields=id,variants&published_status=" & pub_stat & "&limit=250&page=" & pg,a_req,a_reply);

        jdata = ParseJSONArea(a_reply);

        loopc = loopc + 1;
      end;

    end else begin
      MessageBox(0,"ERROR: API Access data must be provided. Check the Shopify settings.");
    end;
  end;
  if (RepSpec.ArtMode == 1) then begin
    while (LoopMain(INr,1,true)) begin
      RecordCopy(IN2r,INr);
      INr.ShopifyID = "";
      RecordUpdate(IN2r,INr,false);
    end;
  end;

return;
end;