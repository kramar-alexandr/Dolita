external inner function Date DateFromString(string,string);
external inner function Time TimeDiff(Time,Time);

procedure AddDuration(Time st,Time et,var duration res)
begin
  Time t,rest;
  
  rest = TimeDiff(st,et);  
  res.hour = res.hour + rest.hour;
  res.minute = res.minute + rest.minute;
  res.second = res.second + rest.second;
  return;
end;

global
procedure DOL_EmpTimeRn(record RcVc RepSpec)
begin
  record POSClockInOutVc PCr;
  Boolean TrHs,testf;
  string 255 datestr,tmp;
  Date td;
  Longint pos;
  vector time vClockIn,vClockOut;
  array string 255 aClockIn;
  Integer i;
  Time diff;
  record UserVc Userr;
  vector string 255 vLocation;
  duration totdur;
  
  StartReportJob(UsetStr(1000149));

  EndHeader;

  SetRepCol(2,70);
  SetRepCol(3,120);
  SetRepCol(4,160);
  SetRepCol(5,280);
  SetRepCol(6,350);
  SetRepCol(7,420);
  
  StartFormat(15);
  OutString(0,0,USetStr(14281),false);//date
  OutString(2,0,USetStr(1000158),false);//location
  OutString(3,0,USetStr(1000159),false);//user id
  OutString(4,0,USetStr(1000160),false);//user name
  OutString(5,0,USetStr(1000161),false);
  OutString(6,0,USetStr(1000162),false);
  OutString(7,0,USetStr(1000163),false);
  EndFormat;
  StartFormat(1);
  Gray_Divider(0,1);
  EndFormat;

  totdur.hour = 0;
  totdur.minute = 0;
  totdur.second = 0;

  TrHs = true;
  PCr.TransDate = RepSpec.sStartDate;
  while (LoopKey("TransDate",PCr,1,TrHs)) begin
    if (PCr.TransDate>RepSpec.sEndDate) then begin
      TrHs = false;
    end else begin
      testf = true;
      if (nonblank(RepSpec.f2) and RepSpec.f2!=PCr.LocalMachine) then begin
        testf = false;
      end;
      if (nonblank(RepSpec.f1) and RepSpec.f1!=PCr.UserCode) then begin
        testf = false;
      end;
      if (testf) then begin
        datestr = DateToString(PCr.TransDate,"YYYY-MM-DD");
        switch (PCr.Type) begin
          case 1: vClockIn[datestr & ":" & PCr.UserCode] = PCr.TransTime;
          case 0: vClockOut[datestr & ":" & PCr.UserCode] = PCr.TransTime;
        end;
        vLocation[datestr & ":" & PCr.UserCode] = PCr.LocalMachine;
      end;
    end;
  end;

  GetVectorTags(vClockIn,aClockIn);
  SortStringArray(aClockIn);
  for (i=0;i<aClockIn.length;i=i+1) begin
    pos = -1;
    GetNextSubString(aClockIn[i],pos,":",tmp);
    td = DateFromString(tmp,"YYYY-MM-DD");
    GetNextSubString(aClockIn[i],pos,":",tmp);
    Userr.Code = tmp;
    ReadFirstMain(Userr,1,true);
    diff = TimeDiff(vClockIn[aClockIn[i]],vClockOut[aClockIn[i]]);
    AddDuration(vClockIn[aClockIn[i]],vClockOut[aClockIn[i]],totdur);

    StartFormat(15);
    OutString(0,0,td,false);
    OutString(2,0,vLocation[aClockIn[i]],false);
    OutString(3,0,Userr.Code,false);
    OutString(4,0,Userr.Name,false);
    OutString(5,0,vClockIn[aClockIn[i]],false);
    OutString(6,0,vClockOut[aClockIn[i]],false);
    OutString(7,0,diff,false);
    EndFormat;

  end;
  
  StartFormat(1);
  Gray_divider(0,1);
  EndFormat;
  StartFormat(15);
  OutString(0,0,"",false);
  OutString(2,0,"",false);
  OutString(3,0,"",false);
  OutString(4,0,"",false);
  OutString(5,0,"",false);
  OutString(6,0,"",false);
  OutString(7,0,left("00",2-len(totdur.hour)) & totdur.hour & ":" & left("00",2-len(totdur.minute)) & totdur.minute & ":" & left("00",2-len(totdur.second)) & totdur.second,false);//hw doesn't know how to output duration...
  EndFormat;

  EndJob;

  return;
end;