
procedure POSButtonsDClassSpecPasteNameCode(Integer wn,var string psname)
begin
  record POSButtonsVc POSBr;
  row POSButtonsVc POSBrw;
  Integer rownr;

  GetWindowRecord(wn,POSBr);
  rownr = WindowActiveRow(wn);
  if (rownr>=0) then begin
    MatRowGet(POSBr,rownr,POSBrw);
  end;
  switch (POSBrw.ButtonType) begin
    case kPOSButtonTypeItem: psname = "ForSaleINSClass";
    case kPOSButtonTypeItemGroup: psname = "ITSClass";
    case kPOSButtonTypeFullPayment: psname = "RestPMSClass";
    case kPOSButtonTypeCashPayment: psname = "RestPMSClass";
    case kPOSButtonTypeLoyaltyPointsPayment: psname = "RestPMSClass";
    case kPOSButtonTypeCreditCardPayment: psname = "RestPMSClass";
    case kPOSButtonTypeCreditCardPaymentReversal: psname = "RestPMSClass";
    case kPOSButtonTypeDebitCardPayment: psname = "RestPMSClass";
    case kPOSButtonTypeOnAccountPayment: psname = "RestPMSClass";
    case kPOSButtonTypeGiftVoucherPayment: psname = "RestPMSClass";
    case kPOSButtonTypeGiftVoucherSales: psname = "RestPMSClass";
    case kPOSButtonTypeChequePayment: psname = "RestPMSClass";
    case kPOSButtonTypeOnHotelGuestAccount: psname = "RestPMSClass";
    case kPOSButtonTypeModifier: psname = "ModifiersSClass";
    case kPOSButtonTypePOSCommand: psname = "SetSClass";
    case kPOSButtonTypeSpecialMenu: psname = "SpecialMenuFunSClass";
    case kPOSButtonTypeReturnInvoiceNo: psname = "StandProblemsSClass";
    case kPOSButtonTypeReturnReason: psname = "StandProblemsSClass";
    case kPOSButtonTypeVoidRow: psname = "StandProblemsSClass";
    case kPOSButtonTypeGotoPage: psname = "POSButtonsPagesSClass";
    case kPOSButtonTypeIncludePage: psname = "POSButtonsPagesSClass";
    otherwise psname = "";
  end;
  return;
end;

global
function string 40 POSButtonsDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 255 psname;
  
  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "Code": POSButtonsDClassSpecPasteNameCode(wn,psname);
  end;
  POSButtonsDClassSpecPasteName = psname;
  return;
end;

global
function Integer POSButtonsDClassSelectSetToPaste(Integer wn,string fieldname)
begin
  Integer res;
  record POSButtonsVc POSBr;
  
  res = -1;
  GetWindowRecord(wn,POSBr);
  switch (fieldname) begin
    case "ButtonType":
      res = 1431; //EBS edit 431 -> 1431
      switch (StringSetFromString(465,POSBr.WindowClass)) begin
        case "RestAccDClass":
          if (IsStandardProduct) then begin
            res = 525;
          end else begin
            res = 494;
          end;
        case "NPTSIVCashDClass":
          if (IsStandardProduct and BuildProductCode<>"PAYYAPERP") then begin
            res = 524;
          end else begin
            res = 1495; //EBS edit 495 -> 1495
          end;
        case "IVDClass":
          res = 518;
      end;
    case "WindowClass":
      if (IsBooks) then begin res = 506; end;
      if (ProgramType==typPOSStandalone) then begin res = 506; end;
  end;
  POSButtonsDClassSelectSetToPaste = res;
  return;
end;

global
function Integer LocalLoginTClassSelectSetToPaste(Integer wn,string fieldname)
begin
  Integer res;
  record LocalLoginBlock LLb;
  
  res = -1;
  GetWindowRecord(wn,LLb);
  switch (fieldname) begin
    case "OnLoginWindow":
      if (ProgramType==typPOSStandalone) then begin res = 507; end;
  end;
  LocalLoginTClassSelectSetToPaste = res;
  return;
end;


global
function Boolean POSButtonsDClassAfterEditField(Integer wn,string fieldname,Integer fn,Integer rownr,Integer changed)
begin
  Boolean res;
  record POSButtonsVc POSBr;
  row POSButtonsVc POSBrw;
  record INVc INr;
  record ITVc ITr;
  
  res = false;
  switch (fieldname) begin
    case "ButtonType": 
      if (rownr>-1 and changed) then begin
        GetWindowRecord(wn,POSBr);
        MatRowGet(POSBr,rownr,POSBrw);
        POSBrw.Label = StringFromSet(431,POSBrw.ButtonType);
        MatRowPut(POSBr,rownr,POSBrw);
        PutWindowRecord(wn,POSBr);
      end;
    case "Code": 
      if (rownr>-1 and changed) then begin
        GetWindowRecord(wn,POSBr);
        MatRowGet(POSBr,rownr,POSBrw);
        switch (POSBrw.ButtonType) begin
          case kPOSButtonTypeGotoPage:
            POSBrw.Label = POSBrw.Code;
            MatRowPut(POSBr,rownr,POSBrw);
            PutWindowRecord(wn,POSBr);
          case kPOSButtonTypeItem:
            INr.Code = POSBrw.Code;
            ReadFirstMain(INr,1,true);
            POSBrw.Label = INr.Name;
            MatRowPut(POSBr,rownr,POSBrw);
            PutWindowRecord(wn,POSBr);
          case kPOSButtonTypeItemGroup:
            ITr.Code = POSBrw.Code;
            ReadFirstMain(ITr,1,true);
            POSBrw.Label = ITr.Comment;
            MatRowPut(POSBr,rownr,POSBrw);
            PutWindowRecord(wn,POSBr);
        end;
        res = true;
      end;
  end;

  POSButtonsDClassAfterEditField = res;
  return;
end;
