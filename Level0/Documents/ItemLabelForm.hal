external procedure PortugueseFormFields(Integer);
external procedure NextM4SerialNumber(string,var string);
external function roundmode SetRoundModeD(Integer);
external function val MulWithRateToBase2(var string,Date,val,roundmode);
external function val MulWithRateToBase1(var string,Date,val,roundmode);
external procedure CommonDocumentFields(record RcVc);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external function Boolean GetPurchaseItem(string,string,var record PIVc);
external procedure GetBaseCurncy(Integer,var string);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external procedure ItemBCEAN(string,var string);
external procedure ItemBCEAN13(string,var string);
external procedure ItemBC39(string,var string);
external procedure GetBaseCurncy(Integer,var string);
external function Boolean FindItemVAR(string,var string,var string,var string,var string); //EBS
external procedure SplitVars(string,Array string,Array string,Array string); //EBS
external outer function string 20 GetVarietyBarCode(string,string); //EBS

procedure ItemLabel(record DocVc Docr,record RcVc RepSpec,record INVc INr,record LangNrVc LangNrr,record SysFormatBlock SysFormatRec,string serialnr)
BEGIN
  record VARVc VARr;
  string 255 tstr;
  val t;
  record PIVc PIr;
  Integer i,rwcnt;
  longint pos;
  row DocVc Docrw;
  record ItemSettingBlock ISbl;
  string 20 oclass,b1;
  record DIVc DIr;
  record CUVc VEr; //EBS
  
  GetBaseCurncy(1,b1);
  BlockLoad(ISbl);
  
  if (HasLocalization("PRT")) then begin
    PortugueseFormFields(17168);
  end;
  ItemBC39(INr.Code,tstr);
  OUTFORMFIELD("F_ARTNRBC39",tstr);
  ItemBCEAN(INr.Code,tstr);
  OUTFORMFIELD("F_ARTNRBCEAN",tstr);
  ItemBCEAN13(INr.Code,tstr);
  OUTFORMFIELD("F_ARTNRBCEAN13",tstr);  

  OUTFORMFIELD("F_ARTNR",INr.Code);
  // OUTFORMFIELD("F_ARTNR2",INr.Code);
  OUTFORMFIELD("F_SPECIFIKATION",INr.Name);
  OUTFORMFIELD("F_ENHET",INr.Unittext);
  OUTFORMFIELD("F_GRUPP",INr.Group);
  // OUTFORMFIELD("F_OBJECT",INr.Objects);
  // OUTFORMFIELD("F_AVDELNING",INr.Department);
  OUTFORMFIELD("F_LAGERPLATS",INr.InvCode);
  OUTFORMFIELD("F_EUCODE",INr.EUCodex);
  OUTFORMFIELD("F_PRICECHANGE",INr.LastPriceChange);
  PrintValue("F_BASPRIS",INr.UPrice1,M4Val,LangNrr,SysFormatRec,false);
  PrintValue("F_HALINITPRICE",INr.InitPrice,M4Val,LangNrr,SysFormatRec,false);
  OUTFORMFIELD("F_TRANSDATE",INr.LastBasePriceChange);
  OUTFORMFIELD("F_ITEMCODE",INr.AlternativeCode);
  //EBS-->
  OUTFORMFIELD("F_AVDELNING",INr.Code);
  if nonblank(RepSpec.f12) then begin
    OUTFORMFIELD("F_BARCODE",GetVarietyBarCode(INr.Code,RepSpec.f12));
  end else begin
    OUTFORMFIELD("F_BARCODE",INr.BarCode);
  end;
//EBS<--
  GetBaseCurncy(1,tstr);
  if (nonblank(tstr)) then begin 
    t = MulWithRateToBase2(tstr,CurrentDate,INr.UPrice1,SetRoundModeD(3)); 
  end else begin
    t = INr.UPrice1;
  end;  
  PrintValue("F_PRICEBASECURNCY2",t,M45Val,LangNrr,SysFormatRec,false);
  if (GetPurchaseItem(INr.Code,"",PIr)) then begin 
//EBS-->
    VEr.Code = PIr.VECode;
    if ReadFirstMain(VEr,1,true) then begin
      OUTFORMFIELD("FF_VEADDR1",VEr.InvAddr0);
      //OUTFORMFIELD("FF_VEADDR2",VEr.InvAddr1); //OLD
      tstr = "";
      if(nonblank(VEr.InvAddr3))then begin
      	tstr = VEr.InvAddr3;
      end;
      if(nonblank(VEr.InvAddr4))then begin
      	if(nonblank(tstr))then begin
      		tstr = tstr & ", " & VEr.InvAddr4;
      	end else begin
					tstr = VEr.InvAddr4;
      	end;
      end;
      if(nonblank(VEr.InvAddr2))then begin
      	if(nonblank(tstr))then begin
      		tstr = tstr & ", " & VEr.InvAddr2;
      	end else begin
					tstr = VEr.InvAddr2;
      	end;
      end;
      OUTFORMFIELD("FF_VEADDR2",tstr);
      OUTFORMFIELD("FF_VEADDR3",VEr.InvAddr2);
      OUTFORMFIELD("FF_VEADDR4",VEr.InvAddr3);
      OUTFORMFIELD("FF_VEADDR5",VEr.InvAddr4);
      OUTFORMFIELD("FF_VENAME",VEr.Name);
    end;
//EBS<--
  end;
  OUTFORMFIELD("F_UNITCODE",PIr.VEUnit);
  OUTFORMFIELD("F_ARTNR2",PIr.VEName); //EBS
  OUTFORMFIELD("F_SERIALNR",serialnr);

  ItemBC39(serialnr,tstr);
  OUTFORMFIELD("F_SERIENRBC39",tstr);
  ItemBCEAN(serialnr,tstr);
  OUTFORMFIELD("F_SERIENRBCEAN",tstr);
  ItemBCEAN13(serialnr,tstr);
  OUTFORMFIELD("F_SERIENRBCEAN13",tstr);
  CommonDocumentFields(RepSpec);
//EBS-->
  if nonblank(RepSpec.f12) then begin
    VARr.Code = RepSpec.f12;
    ReadFirstMain(VARr,1,true);
    OutFormField("F_OBJECT",VARr.Comment);
  end;
//EBS<--
  // EVS: DOL-33
  OutFormField("F_HALB1CUR",b1);
  OutFormField("F_HALVARCODE",RepSpec.f12);
  pos = 0;
  while (GetNextSubstring(INr.DispGroups,pos,",",oclass)) begin
    DIr.Code = oclass;
    if (ReadFirstMain(DIr,1,true)) then begin
      if (DIr.CType == ISbl.ItemLabelCTypeCode) then begin
        OutFormField("F_HALDISPGROUPNAME",DIr.Name);
        goto LItemLabel;
      end;
    end;
  end;
  LItemLabel:;
  // :EVS

  rwcnt = MatRowCnt(Docr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Docr,i,Docrw);
    if (Docrw.fieldSetNr==0) then begin
      OUTFORMFIELD(Docrw.unitText,Docrw.unitText);
    end;
  end;    
  RETURN;  
END;
  
procedure DoItemLabelFormPrint(record RcVc RepSpec,string formcode,string printer)
BEGIN
  record INVc INr;
  string 255 fromitem,toitem,ckey;
  string 255 frserialnr,toserialnr,serialnr;
  Boolean TrHs;
  LongInt incnt;
  Integer keys,lbls;
  LongInt i,times;
  Boolean testf;
  Boolean formisopen;
  Integer maxcps;
  Integer curcp;
  record SysFormatBlock SysFormatRec;
  record LangNrVc LangNrr;
  record DocVc Docr;
//EBS-->
  string 255 mask,itemsize,varsubset,mskrep;
  Array string 255 maskvar,maskvarsz,amsk;
//EBS<--

  GetLangNr("",LangNrr);
  BlockLoad(SysFormatRec);
  maxcps = 1;
  curcp = 1;
  if (OpenForm(formcode)==false) then begin goto L99; end;
  SetPrinter(printer);  
  Docr.Code = formcode;
  if (ReadFirstMain(Docr,1,true)) then begin end;
  formisopen = true;
  switch (RepSpec.ArtMode) begin
    case 0: FormLabelGrid(1,1,1,1); maxcps = 1;
    case 1: FormLabelGrid(2,200,8,98); maxcps = 16;
    case 2: FormLabelGrid(3,195,8,98); maxcps = 24;
    case 3: FormLabelGrid(5,112,13,65); maxcps = 65;
    case 4: FormLabelGrid(2,305,7,112); maxcps = 14;
    case 5: FormLabelGrid(2,280,4,206); maxcps = 8;
  end;
//EBS-->
  if (FindItemVAR(RepSpec.f1,itemsize,mask,mskrep,varsubset)) then begin
    SplitVars(RepSpec.f1,maskvar,maskvarsz,amsk);
    RepSpec.f1 = amsk[0];
    RepSpec.f12 = amsk[1];
  end;
//EBS<--
  fromitem = FirstInRange(RepSpec.f1,20);
  toitem = LastInRange(RepSpec.f1,20);
  frserialnr = FirstInRange(RepSpec.f3,60);
  toserialnr = LastInRange(RepSpec.f3,60);
  times = RepSpec.long1;
  if (times<1) then begin
    times = 1;
  end;
  if (RepSpec.flags[1]==1) then begin
    INr.Name = fromitem;
    INr.Code = "";
    ckey = "Name";
    keys = 1;
  end else begin
    if (blank(RepSpec.f2)) then begin
      INr.Code = fromitem;
      ckey = "Code";
      keys = 1;
    end else begin
      INr.Group = RepSpec.f2;
      INr.Code = fromitem;
      ckey = "Group";
      keys = 2;
    end;
  end;  
  TrHs = true;
  while (LoopKey(ckey,INr,1,TrHs)) begin  
    if (TrHs) then begin
      testf = true;
      if (RepSpec.flags[1]==1) then begin
        if (nonblank(toitem)) then begin
          if (INr.Name>toitem) then begin
            TrHs = false;
          end;
        end;
      end else begin
        if (nonblank(RepSpec.f2)) then begin
          if (INr.Group!=RepSpec.f2) then begin
            TrHs = false;
          end;
        end;
        if (nonblank(toitem)) then begin
          if (INr.Code>toitem) then begin
            TrHs = false;
          end;
        end;
      end;
      if (nonblankdate(RepSpec.d1)) then begin
        if (RepSpec.d1!=INr.LastBasePriceChange) then begin testf = false; end;
      end;
    end;
    if (TrHs and testf) then begin
      if (nonblank(RepSpec.f3)) then begin
        serialnr = frserialnr;
        while (serialnr<=toserialnr) begin
          for (i=0;i<times;i=i+1) begin
            if (formisopen==false) then begin
              if (OpenForm(formcode)==false) then begin goto L99; end;
              SetPrinter(printer);  
              switch (RepSpec.ArtMode) begin
                case 0: FormLabelGrid(1,1,1,1); maxcps = 1;
                case 1: FormLabelGrid(2,200,8,97); maxcps = 16;
                case 2: FormLabelGrid(3,195,8,97); maxcps = 24;
                case 3: FormLabelGrid(5,112,13,65); maxcps = 65;
                case 4: FormLabelGrid(2,305,7,112); maxcps = 14;
                case 5: FormLabelGrid(2,280,4,206); maxcps = 8;
              end;
              formisopen = true;
            end;
            ItemLabel(Docr,RepSpec,INr,LangNrr,SysFormatRec,serialnr);
            EndFormLabel;
            if (curcp>=maxcps) then begin
              CloseForm;
              formisopen = false;
              curcp = 1;
            end else begin
              curcp = curcp+1;
            end;
          end;
          NextM4SerialNumber(serialnr,serialnr);
        end;      
      end else begin
        for (i=0;i<times;i=i+1) begin
          if (formisopen==false) then begin
            if (OpenForm(formcode)==false) then begin goto L99; end;
            SetPrinter(printer);  
            switch (RepSpec.ArtMode) begin
              case 0: FormLabelGrid(1,1,1,1); maxcps = 1;
              case 1: FormLabelGrid(2,200,8,97); maxcps = 16;
              case 2: FormLabelGrid(3,195,8,97); maxcps = 24;
              case 3: FormLabelGrid(5,112,13,65); maxcps = 65;
              case 4: FormLabelGrid(2,305,7,112); maxcps = 14;
              case 5: FormLabelGrid(2,280,4,206); maxcps = 8;
            end;
            formisopen = true;
          end;
          ItemLabel(Docr,RepSpec,INr,LangNrr,SysFormatRec,"");
          EndFormLabel;
          if (curcp>=maxcps) then begin
            CloseForm;
            formisopen = false;
            curcp = 1;
          end else begin
            curcp = curcp+1;
          end;
        end;
      end;
    end;
  end;
  if (formisopen) then begin
    CloseForm;
  end;
L99:;
  RETURN;
END;  

global
procedure DoItemLabelForm(record RcVc Dummyr,record RcVc RepSpec)
BEGIN
  Integer i,rwcnt;  
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;
  record FormDefVc FDr;
  row FormDefVc FDrw;

  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoItemLabelForm;
  end;
  rwcnt = MatRowCnt(FDr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,-1,"",
                      "",intdocnr,"",formcode)) then
      begin
        goto LBREAKItemLabel;
      end;
    end;
LBREAKItemLabel:;
    if (nonblank(formcode)) then begin
      DoItemLabelFormPrint(RepSpec,formcode,FDrw.PrintGroupCode);
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoItemLabelForm:;  
  RETURN;
END;  

global
procedure ItemLabelForm(record RcVc RepSpec)
BEGIN
  Boolean tmpf;
  
  tmpf = PrintDocument(RepSpec,"ItemLabelForm",false);
  RETURN;
END;
